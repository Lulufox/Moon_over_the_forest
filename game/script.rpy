# Вы можете расположить сценарий своей игры в этом файле.

# Определение персонажей игры.
define nobody = Character(window_background=None)
define narrator = Character(what_italic=True, what_color="#EDDB93")
define you = Character('Вы',color='#A9A490')
define gyn = Character('Гаянэ', color='#B34629', image='gyn')
define rdn = Character('Родион', color ='#B38C29', image = 'rdn')
define ann = Character('Анна Михайловна', color ='#A96467', image = 'ann')
define dar = Character('Даша', color ='#B8C184', image = 'dar')
define tsh = Character('Тшилаба', color = '#7D587E', image = 'tsh')
define yan = Character('Яноро', color = '#BDB56B', image = 'yan')


#https://www.renpy.org/doc/html/dialogue.html Say with Image Attributes

# Вместо использования оператора image можете просто
# складывать все ваши файлы изображений в папку images.
# Например, сцену bg room можно вызвать файлом "bg room.png",
# а eileen happy — "eileen happy.webp", и тогда они появятся в игре.
image blackness = "#000000"
image 8hr = Text('8 часов спустя', size = 40)
image lab_text = Text('Чтобы осмотреть конкретную деталь обстановки, нажмите на нее мышкой.', size = 21, color='#EDDB93')

image message_evidence = Composite(( 460, 84),
  (-15,-16), 'message bg',
  (0,0), 'lab_text')

# Трансформации и проч.
transform slightleft:
    xalign 0.25
    yalign 1.0
transform slightright:
    xalign 0.75
    yalign 1.0

define openeyes = ImageDissolve("open eyes.png", .8, ramplen=1024)

# Флажки
define glass_switch = False
define shawl_switch = False
define table_switch = False
define window_switch = False


screen laboratory():
    imagemap:
        idle "bg laboratory with_shawl"

        if window_switch == False:
            hotspot (741, 39, 331, 366):
                action Jump('broken_window')
                hovered Show('window_highlight')
                unhovered Hide ('window_highlight')
        if table_switch == False:
            hotspot (421, 421, 209, 27):
                action Jump('broken_table')
                hovered Show ('table_highlight')
                unhovered Hide ('table_highlight')
        if shawl_switch == False:
            hotspot (452, 540, 117, 38):
                action Jump('broken_shawl')
                hovered Show ('shawl_highlight')
                unhovered Hide ('shawl_highlight')
        if glass_switch == False:
            hotspot (648, 603, 215, 117):
                action Jump('broken_glass')
                hovered Show ('glass_highlight')
                unhovered Hide ('glass_highlight')


label broken_window:
    hide screen window_highlight with dissolve
    narrator 'Вы обращаете внимание на сильно поврежденное окно: выбитое стекло, сломанный средник, порванные занавески.'
    you 'Хмм... Похоже, злоумышленник использовал окно вместо двери. Выходит, он не боялся, что снаружи его увидят.'
    show rdn sad at left with dissolve
    rdn sad 'Это окно выходит на тропинку, ведущую к задним воротам усадьбы. Не думаю, что кто-то ходит по ней по ночам.{w}..{p}...'
    rdn sad_side 'Если так подумать, по ней и днем то редко кто ходит. Эта тропинка проходит мимо черного хода усадьбы и ведет к дому Гаянэ в лесу за пределами поместья. А больше там и нет никаких строений.'
    you 'Хмм...'
    you 'У кого могло хватить силы, чтобы настолько сильно повредить окно?'
    rdn sad 'Боюсь даже представить...'
    $ window_switch = True
    if not glass_switch == shawl_switch == table_switch == True:
        hide rdn with dissolve
        narrator 'Родион отходит обратно к дверям, давая Вам возможность продолжить осмотр.'
        jump laboratory_return
    elif window_switch == table_switch == shawl_switch == glass_switch == True:
        jump broken_shawl
label broken_table:
    hide screen table_highlight with dissolve
    narrator 'Вы принимаетесь рассматривать предметы на столе. Пробирки и колбы подсказывают Вам, что Родион занимался здесь изготовлением некой химической субстанции.'
    you 'Если не секрет, что ты здесь готовил? Лекарство от ликантропии?'
    show rdn happy at left with dissolve
    rdn happy 'Не издевайся!'
    rdn neutral 'Да, я занимаюсь поиском лечения этой болезни.'
    you 'Ты встречал хоть одного человека, который бы ею болел?'
    rdn sad_side 'Ну... Я встретился с тем арабом, о котором как-то писал тебе.'
    you 'Это который излечился?'
    rdn sad 'Да.'
    you 'И он?..'
    rdn sad_side 'И, конечно, он выглядел как обычный человек...'
    you '...'
    you 'Так как ты намерен приготовить лекарство, не имея больного?'
    rdn sad 'Ну, я пытаюсь воспроизвести один старый рецепт по книгам и прочим... источникам.'
    you 'А что потом? Как ты намерен проверить, работает твое лекарство или нет?'
    rdn neutral 'Граф финансирует мои исследования. Он сказал, что кто-то из его высокопоставленных друзей нуждается в этом.'
    you 'Ты веришь этому?'
    rdn sad 'Не знаю. Ну то есть я верю, что больной существует. Но я немного сблизился с графом - он очень добрый человек.'
    rdn neutral 'Он вполне мог бы заказать такое исследование для кого-то из деревенских, а про высокое положение сказать просто, чтобы не выдавать человека.'
    you 'Ты правда думаешь, что это разумно? Не выдавать ликантропа? Если он и правда существует, то это же огромный неуправляемый зверь.'
    rdn sad_side 'Ну, если тут в Тополево и правда есть свой ликантроп, то он пока никого не убил. Зачем отдавать его на расправу, если он просто больной человек?..'
    narrator 'Вам не слишком нравится этот романтический тон.'
    $ table_switch = True
    if not glass_switch == shawl_switch == window_switch == True:
        hide rdn with dissolve
        narrator 'Родион отходит к дверям, чтобы не загораживать свет.'
        jump laboratory_return
    elif window_switch == table_switch == shawl_switch == glass_switch == True:
        jump broken_shawl
label broken_glass:
    hide screen glass_highlight with dissolve
    narrator 'Ваше внимание привлекает пол под окном.'
    you 'Странно...'
    you 'На полу нет осколков.'
    show rdn sad at left with dissolve
    rdn sad 'Как такое может быть?'
    you 'Элементарно, дорогой друг!'
    you 'Если стекла нет на полу комнаты, значит это самое стекло выбивали не снаружи, а изнутри.'
    narrator 'Вы на секунду выглядываете из окна и ожидаемо находите осколки на траве под окном.'
    you 'Злоумышленник не заходил через окно, но он через него вышел.'
    rdn shock 'Но это же получается...'
    you 'Верно, что он вошел через дверь.'
    rdn sad 'Невозможно...'
    $ glass_switch = True
    if not table_switch == shawl_switch == window_switch == True:
        hide rdn with dissolve
        narrator 'Родион задумывается и медленно отступает к двери.'
        jump laboratory_return
    elif window_switch == table_switch == shawl_switch == glass_switch == True:
        jump broken_shawl
label broken_shawl:
    hide screen shawl_highlight with dissolve
    if table_switch == glass_switch == window_switch == True:
        if shawl_switch == True:
            narrator 'Кажется, здесь больше не на что смотреть.'
            narrator 'Вы вновь возвращаетесь к платку, на который обратили внимание ранее.'
        else:
            narrator 'Вы замечаете под столом яркий кусок ткани.'
            narrator 'Присмотревшись, Вы опознаете в нем женский платок.'
        hide rdn with dissolve
        jump laboratory_done
    else:
        $ shawl_switch = True
        narrator 'Вы замечаете под столом яркий кусок ткани.'
        narrator 'Присмотревшись, Вы опознаете в нем женский платок.'
        narrator 'Вы задумываетесь о том, что женский платок в лаборатории Родиона - это что-то {b}из ряда вон выходящее{/b}. Вы решаете вернуться к нему после того, как осмотрите все помещение.'
        jump laboratory_return
label laboratory_return:
    call screen laboratory


screen window_highlight():
    imagemap:
        idle 'window_highlight'
screen table_highlight():
    imagemap:
         idle 'table_highlight'
screen glass_highlight():
    imagemap:
         idle 'glass_highlight'
screen shawl_highlight():
    imagemap:
         idle 'shawl_highlight'

# Письма

screen sc_letter1():
    imagemap:
        idle 'letter1_idle'
        hover 'letter1_hover'

        hotspot(945, 336, 58, 29):
            action Jump('letter2')
screen sc_letter2():
    imagemap:
        idle 'letter2_idle'
        hover 'letter2_hover'

        hotspot(326, 327, 51, 26):
            action Jump('letter1')
        hotspot(945, 336, 58, 29):
            action Jump('letter3')
screen sc_letter3():
    imagemap:
        idle 'letter3_idle'
        hover 'letter3_hover'

        hotspot(326, 327, 51, 26):
            action Jump('letter2')
        hotspot(945, 336, 58, 29):
            action Jump('letter4')
screen sc_letter4():
    imagemap:
        idle 'letter4_idle'
        hover 'letter4_hover'

        hotspot(326, 327, 51, 26):
            action Jump('letter3')
        hotspot(945, 336, 58, 29):
            action Jump('letters_done')



# Игра начинается здесь:
label start:

    play music "opening.mp3" fadein 2

    scene moon_and_forest
    window hide #https://www.renpy.org/doc/html/dialogue.html
    pause .5
    scene moon_and_forest at Zoom((1280, 720), (0, 0, 1280, 720), (300, 0, 950, 534), 5.0)
    pause 5



    show blackness with fade

    show trees behind blackness:
        xpan -20
        linear 90 xpan 360
    show smoke behind trees:
        linear 100 xpan -360
    show bg_window behind smoke
    pause .3
    hide blackness with dissolve
    pause 14
    show figure:
        xpos 1200 ypos -384
        linear .5 xpos -1500 ypos 120
    pause 2
    scene blackness with fade
    stop music fadeout 8
    pause 8
    show 8hr with fade:
        yalign .5
        xalign .5
    play music "поездка в дилижансе.mp3" fadein 4
    pause 3
    $ quick_menu = True

    scene bg diligence with openeyes
    pause 1
    narrator 'Вы открываете глаза и жмуритесь от яркого света - уже давно рассвело.'
    narrator 'Чувствуете, что всё тело затекло. Вы привыкли к не слишком комфортным условиям путешествий, но всё же спать сидя - то ещё удовольствие.'
    narrator 'Слышен мерный цокот копыт лошадей почтового дилижанса. Вы про себя отмечаете, как Вам повезло, что удалось разыскать транспорт, который доставил бы Вас в такую глушь.'
    narrator 'Вы смотрите на часы. Половина десятого. Через полчаса дилижанс должен добраться до села Тополево, куда и лежит ваш путь.'
    narrator 'Вы потягиваетесь, достаёте из внутреннего кармана плаща потрепанное письмо и в который раз перечитываете его.'
    call screen sc_letter1 with dissolve
    label letter1:
        call screen sc_letter1
        hide screen sc_letter2
    label letter2:
        call screen sc_letter2
        hide screen sc_letter1
        hide screen sc_letter3
    label letter3:
        call screen sc_letter3
        hide screen sc_letter2
        hide screen sc_letter4
    label letter4:
        call screen sc_letter4
        hide screen sc_letter3
    label letters_done:
        show letter4_pic
        hide screen sc_letter4
        hide letter4_pic with dissolve

    narrator 'Вы складываете письмо и убираете его.'
    narrator 'За этим письмом следовало много других, Родион очень заинтересовался темой ликантропии: присылал вырезки из статей и памфлетов, цитаты из каких-то старых книг, зарисовки.'
    narrator 'После окончания военной кампании Вы потеряли связь с другом - он не успел написать, куда отправится после возвращения.'
    narrator 'Однако, Вы всегда отличались остротой ума и умением находить логические связи, поэтому подсказки, оставленные Родионом - его предполагаемое путешествие по Османской империи после окончания боев...'
    narrator '...  и намерение изучать ликантропию - помогли Вам выйти на его след.'
    narrator 'После возвращения на родину Родиона взял под покровительство некто граф Озерский, воевавший на том же театре военных дествий. Он согласился финансировать исследования вашего друга и предоставить ему лабораторию.'
    narrator 'Из собранной Вами информации следовало, что граф после войны переехал в усадьбу в селе Тополево, там же и вел исследования Ваш друг.'
    narrator 'И вот, наконец, Ваша собственная работа по вакцинации крестьянского и городского населения привела Вас в эти края...'
    narrator '... и Вы немедленно воспользовались возможностью навестить друга. Вы поспешно связались с ним и сообщили о своем скором приезде.'
    narrator 'Дилижанс слегка подпрыгивает на кочке. Этого оказывается достаточно, чтобы вырвать Вас из Ваших раздумий.' with vpunch
    narrator 'Из окна Вы замечаете небольшой дом. Это, должно быть, почтовая станция Тополево.'
    stop music fadeout 4
    narrator 'Вы поправляете одежду, дожидаетесь, пока дилижанс остановится, и выходите на улицу.'

    play music "Тополево.mp3" fadein 3
    scene bg post day with dissolve
    show gyn neutral_side at slightright with dissolve
    narrator 'Около здания станции Вы замечаете хмурую девушку с ружьем. Вы отмечаете, что она немного странно одета.'
    show gyn neutral
    narrator 'Наконец незнакомка обращает на Вас внимание и, как ни странно, сама начинает разговор.'
    gyn 'Доброе утро. Вы, видимо, Дмитрий Васильевич. Мое имя Гаянэ, можно без отчества.'
    you 'Доброе. Простите, я ожидал, что меня будет встречать сам Родион.'
    gyn 'Он опаздывает. Я буду сопровождать Вас на время Вашего визита. Распоряжение графини.'
    you 'Графиня считает, что я могу представлять опасность?'
    gyn think 'Наоборот, графиня печется о Вашей безопасности. У нас тут... кхм... кое-что происходит.'
    show gyn neutral
    you 'Что происходит? О чем Вы?'
    gyn angry 'Ночью кто-то пробрался в усадьбу и кое-что украл. {p}Но народ любит сочинять небылицы...'

    narrator 'Гаянэ осторожно подбирает слова, но не успевает закончить мысль.'

    show rdn shock at slightleft with dissolve
    show gyn neutral
    rdn 'Дим, доброе утро!'
    narrator 'Родион выглядит запыхавшимся и немного растрепанным, он явно бежал.'
    rdn happy 'Прости, что опоздал. Тут такой бардак, я совсем забыл о времени.'
    you 'Рад тебя видеть!'
    narrator 'Вы с Родионом обнимаетесь и перекидываетесь парой бессмысленных комплиментов.'
    you 'Гаянэ как раз говорила, что у Вас что-то стряслось, отчего мне нужна личная охрана.'
    show rdn sad_side
    show gyn neutral_side
    narrator 'Родион осторожно поглядывает на Гаянэ. Та не отводит взгляд, будто предлагая ему объясниться самостоятельно.'
    rdn shock 'Да...эээ... В общем, сегодня ночью кто-то вломился в мой кабинет и кое-что украл.'
    show rdn sad_side
    show gyn neutral
    narrator 'Родион смущенно отводит глаза.'
    you 'Что украл?'
    narrator 'Родион нервно сглатывает.'
    show rdn sad
    rdn 'Эээ... Одну вещь...эээ. Браслет, я привез его с востока. Он драгоценный.'
    show rdn angry
    narrator 'Лицо Родиона искажается, голос становится более уверенным и даже грозным.'
    rdn angry 'Да. Серебряный браслет. Я уверен, что это дело рук цыган!'
    you 'Цыган? У Вас здесь есть цыгане?'
    gyn angry 'Да, к востоку от усадьбы цыганская стоянка. Они тут с позволения графа...'
    narrator 'Вы замечаете, что Гаянэ отвечает немного раздраженно, будто ей неприятно даже говорить о цыганах.'
    rdn 'Да, они пользуются милостью Павла Ивановича. Но после этой выходки им не сдобровать! Где это видано - пробраться в усадьбу и вломиться в мой кабинет!'
    you 'Твой кабинет уже убрали?'
    show gyn shock
    rdn shock 'Что?'
    you 'Если там ничего не трогали, могу я взглянуть на это место? Возможно, по уликам удастся определеить, кто это сделал.'
    rdn happy 'А, узнаю тебя! Да, конечно, там почти ничего не трогали.'
    show gyn neutral
    rdn neutral 'Мы можем посмотреть, но я уверен, что это цыгане...'
    hide rdn with dissolve
    hide gyn with dissolve
    narrator 'Вы втроем уходите по проселочной дороге в сторону усадьбы.'

    scene bg mansion1 day with dissolve
    narrator 'Дорога выводит вас к крупному строению, окруженному более мелкими гостевыми домами. Само здание усадьбы выглядит так, будто еще недавно оно было заброшено.'
    you 'Не поймите меня неправильно, но разве это место не слишком... запущенное для графа? Я посещал несколько графских усадеб во время работы - большинство из них были похожи на небольшие дворцы, а здесь...'
    show rdn sad at slightleft with dissolve
    rdn sad 'Да, пожалуй, но это старинный дом. Деревня здесь тоже небольшая. Я думаю, граф осознанно решил поселиться в глуши. Я и сам рад тишине после фронта. Но вот графине здесь явно тесно...'
    show gyn think at slightright with dissolve:
    narrator 'Вы замечаете, как подошедшая следом Гаянэ закатывает глаза.'
    you 'А что за местные небылицы, о которых ты говорила, Гаянэ?'
    gyn neutral 'Местные говорят, что видели здесь волка. {w}Гигантского. {w}Ходящего на двух ногах. {w}Они говорят о волколаке.'
    show rdn shock
    you 'Волколаке? Родион, здесь что, есть настоящий оборотень?!'
    narrator 'Родион теряется и начинает что-то мямлить в ответ.'
    rdn sad_side 'Эээ... Да, может быть. Но его раньше не было и никто не знает, кто...'
    gyn angry 'Глупости все это! Цыганские сказки. До их приезда сюда никто и не говорил о волке, а теперь все постоянно обсуждают эту белиберду!'
    narrator 'Родион достает из кармана связку ключей.'
    rdn sad 'Ты хотел посмотреть на лабораторию, пойдем.'
    show gyn neutral
    hide rdn with dissolve
    narrator 'Гаянэ остается ждать снаружи, пока Родион проводит Вас в свою лабораторию.'

    scene bg laboratory with_shawl with dissolve
    narrator 'В небольшом помещении валяются склянки, штативы для них, стоят сундуки, полки шкафов заполнены старыми книгами. Вы начинаете внимательно осматривать комнату, пока Родион ждет в дверях.'
    show message_evidence with dissolve:
        xalign .5 yalign .5
    nobody ''
    hide message_evidence with dissolve
    call screen laboratory

    # Обследуем лабораторию
    label laboratory_done:
        hide screen laboratory

    scene bg laboratory without_shawl with dissolve
    show shawl with dissolve
    nobody ''

    narrator 'Вы разворачиваете платок в руках: он довольно большой, покрыт цветочным узором и явно чем-то испачкан.'
    narrator 'Фиолетовая субстанция, покрывающая центр платка, источает странный запах, будто сочетающий в себе растительные нотки и спиртовые испарения.'
    you 'Выходит, что это цыганский платок?..'
    hide shawl with dissolve
    show rdn angry at left with dissolve
    rdn 'Да, точно! Подумать только, они оставили здесь прямую улику! Это точно они, говорю тебе.'
    you 'Как говришь, выглядел тот браслет?'
    rdn shock 'Что?.. '
    rdn sad 'А, он, ну был серебряный с бирюзой.'
    you 'Не пойми меня неправильно, но почему ты хранил браслет в лаборатории?..'
    rdn sad_side 'Я... Ну, это очень... личная штука. Я хотел...эээ...'
    rdn sad 'Я хотел... подарить его... одной девушке.'
    you 'Правда? Вот это да. Я то думал, ты решил полностью посвятить себя науке, а до женщин тебе дела нет.'
    show rdn happy
    you 'Так что, пойдем, поговорим с цыганами?'
    rdn shock 'Что?!'
    you 'Ты говоришь, что это они украли браслет. Пойдем и спросим их. К тому же, надо понять, откуда здесь этот платок.'
    rdn sad 'Я...я не думаю, что это безопасно. Они же, ну... Может, сначала посоветоваться с гр...'
    you 'Цыгане и так здесь по милости графа. Ничего они нам не сделают. Мы просто поговорим. К тому же, с нами Гаянэ.'
    narrator 'Вы забираете платок себе и выходите из разгромленной лаборатории.'

    scene blackness with dissolve
    narrator 'Родион разбирается со связкой ключей, пытаясь найти тот, что запирает дверь.'
    scene bg mansion1 day with dissolve
    show gyn neutral_side at right with dissolve
    show ann neutral_side with dissolve:
        xpos .4
        yalign 1.0
    narrator 'Вы застаете Гаянэ беседующей с красивой молодой женщиной.'
    gyn neutral 'А вот и он.'
    show ann neutral
    gyn 'Анна Михайловна, это Дмитрий Васильевич, друг Родиона. Дмитрий, перед Вами графиня Анна Михайловна Озерская, супруга графа.'
    ann happy 'Приятно познакомиться, дорогой Дмитрий. Гаянэ сказала, что Вы пожелали посетить место преступления, как настоящий сыщик.'
    you 'Да, я обожаю загадки, поэтому решил воспользоваться случаем.'
    ann neutral 'Как прелестно. Вам, возможно, захочется также поговорить со свидетельницей.'
    show gyn think
    you 'Свидетельницей? А есть очевидец?'
    ann closed 'Да, Дарья, деревенская девушка. Она рассказала мне, что сегодня ночью кое-что видела.'
    show gyn neutral_side
    ann happy 'Возможно, это добавит красок вашему приключению!'
    narrator 'Вам приходит в голову, что насмешливый тон графини говорит о том, что она не придает особую значимось произошедшему. Похоже, что для нее это какая-то шутка.'

    scene bg mansion2 day with dissolve
    show ann neutral at slightleft with dissolve
    show dar neutral with dissolve
    narrator 'Графиня отводит Вас в сторонку и знакомит с молодой девушкой. Та выглядит смущенной и слегка напуганной.'
    show dar shy
    ann closed 'Даша, расскажи Дмитрию Васильевичу, что ты видела сегодня ночью.'
    show ann happy
    nobody ''
    show ann neutral_side
    nobody ''
    show ann closed
    hide ann with dissolve
    narrator 'Графиня поглаживает девушку по плечу, тепло улыбается ей, после чего замечает вышедшего на улицу Родиона и направляется к нему.'

    you 'Ничего не бойся.'
    dar shock 'Как тут не бояться! Это был волколак! Гигантский, лохматый волколак!'
    show gyn angry at right with dissolve
    narrator 'Гаянэ решительно подходит к Вам с Дашей.'
    gyn 'Вы и правда намерены поверить этим сказкам?!'
    dar angry_side 'Это не сказки! Я возвращалась из леса, несла домой дикие яблоки. Раньше я ходила рядом с твоим, Гайка, домом, но там меня всегда пугала твоя жуткая собака!'
    dar shy 'Вы бы видели её, Дмитрий Василич, огромная злая зверюга! Постоянно пытается укусить!'
    gyn think 'Моя Сойка мухи не тронет, она просто лает, потому что ты по ночам по лесу шляешься!'
    gyn angry 'На этих проклятых цыган она тоже лает, потому что они тоже, вместо того, чтобы спать, ходят и что-то вынюхивают!'
    you 'Барышни, пожалуйста! Я хотел бы услышать рассказ Даши.'
    show gyn think
    dar neutral 'Я ж говорю, я в этот раз решила пойти ближе к усадьбе, чтобы этой зверюге не попасться. Как раз под окнами, где этот доктор работает, увидела что-то большое и лохматое.'
    show gyn neutral
    dar shock 'Эта тварь меня не видела даже! Я сразу на дерево полезла, чтоб точно ей не попасться. Я сначала услышала, как что-то разбилось и грохнуло. А потом уже эту штуку увидала.'
    gyn 'Треск и грохот, наверное, от разбитого окна.'
    gyn angry 'Не подумайте, что я верю во все эти росказни. Но окно то кто-то выломал.'
    show dar shy
    show gyn neutral
    you 'А что было потом?'
    dar 'Да ничего, я притихла на дереве. Оттуда волколака видно не было. Я там, наверное, часа два просидела. А потом спустилась и бегом домой. Больше я ничего и не видела.'
    show dar neutral
    narrator 'Даша замолкает, Вы с Гаянэ тоже не находите, что еще сказать. {p}... {p}Наконец, Вы прерываете молчание.'
    you 'Спасибо, Даша, ты нам очень помогла. Я подумаю обо всем этом. Пока не знаю, что тут можно сказать.'
    dar shy 'Вы только будьте осторожнее, Дмитрий Василич.'
    hide dar with dissolve
    narrator 'Даша прощается с Вами и уходит. Вы возвращаетесь к графине и Родиону.'

    scene bg mansion1 day with dissolve
    show rdn sad_side at right with dissolve
    show ann neutral_side with dissolve:
        xpos .4
        yalign 1.0
    pause .8
    ann neutral 'Ну как, узнали что-нибудь новое?'
    you 'Да, благодарю Вас, Анна Михайловна.'
    show rdn shock
    you 'Мы хотели бы еще сегодня же посетить цыганскую стоянку.'
    show rdn sad
    ann closed 'Вы тоже ценитель этой диковинной культуры?'
    you 'Ох, прошу меня простить, но нет. Видите ли, в кабинете Родиона мы обнаружили цыганский платок. Я хотел бы понять, как он там оказался.'
    show rdn sad_side
    ann happy 'Что ж, понимаю, Вы во всем пытаетесь докопаться до истины.'
    ann closed 'Только прошу Вас, не обижайте их. Это гордый народ, удивительный народ. В такой глуши они, пожалуй, мое единственное развлечение, помимо книг.'
    ann sad 'Иногда так хочется поговорить с интересными людьми, а вокруг один деревенский люд, с ними только скот да урожай обсуждать.'
    show rdn sad
    you 'Я понял Вас, Анна Михайловна. Постараюсь быть предельно тактичным.'
    ann neutral 'Благодарю Вас.'
    ann happy 'Ох, я чуть не забыла. Сегодня вечером мы с графом ждем Вас в усадьбу на ужин.'
    narrator 'Вы благодарите графиню и прощаетесь. Она удаляется в направлении усадьбы.'
    hide ann with dissolve
    pause .4
    show gyn neutral with dissolve:
        xpos .3
        yalign 1.0
    narrator 'Вы, Гаянэ и Родион поворачиваете в противоположную сторону и покидаете территорию поместья.'


    scene blackness with fade
    stop music fadeout 5
    pause 1
    narrator 'По тропке, вьющейся через деревню и поля, а после ныряющей в лес, вы втроем выходите к цыганской стоянке.'

    scene bg camp1 with dissolve
    play music "цыганская стоянка.mp3" fadein 5
    narrator 'Лично Вам раньше не доводилось бывать в таком поселении: на небольной поляне в лесу расставлены палатки и шатры, рядом с некоторыми из них Вы замечаете повозки.'
    narrator 'По лагерю ходит много людей, слышан говор на чужом языке и крики играющих неподалеку детей.'
    show gyn neutral at slightright with dissolve
    you 'Гаянэ, ты упомянула, что разговоры об оборотне появились после того, как приехал табор.'
    gyn 'Да. Вернее, я прибыла сюда по приказу графа через пару недель после того, как они с графиней заехал в усадьбу.'
    gyn angry 'Тогда никаких разговоров о волколаке не было слышно. Что раньше было - я не знаю.'
    show rdn sad_side at slightleft with dissolve
    gyn neutral 'А вот еще через месяц сюда прикачевали эти и началось...'
    gyn 'Я вот как считаю - нет тут никакого волколака. Сказки все это. Может они сами и пустили слухи, чтобы народ боялся по ночам ходить и никто не видел, как они из усадьбы добро выносят.'
    show rdn neutral
    gyn angry 'Собака моя, Сойка, лаять начала по ночам, потому что эти цыгане вокруг усадьбы ходят после заката.'
    you 'Почему ты думаешь, что это они? Даша говорила, что ей тоже приходится там проходить.'
    gyn neutral 'Так я видела одного из них! Если собака поднимает шум, я всегда проверяю, что там. Мало ли, медведь к усадьбе вышел или волки.'
    show rdn sad_side
    gyn 'Так вот, видела я, как там парень шлялся из этих. Несколько раз его замечала. Он как меня услышит, деру дает.'
    gyn neutral_side 'Думаю, это он к Родиону и залез.'
    show rdn sad
    narrator 'Родион внимательно слушает, но в разговор не вмешивается.'
    you 'Хм... Как думаешь, а смогла бы ты узнать этого парня?'
    show gyn shock
    gyn neutral 'Я его в потьмах видела и издалека, но вообще, думаю, да.'
    you 'Хорошо. Дай мне знать, если увидишь его тут.'
    show gyn angry
    show rdn neutral
    you 'И вот еще что...'
    gyn neutral 'Ммм?'
    you 'Собака твоя, она сегодня ночью лаяла, когда кто-то лабораторию разгромил?'
    show gyn think
    pause 1.2
    gyn shock 'Нет... Сегодня не лаяла, все тихо было...'
    show gyn neutral
    you 'Хм...'
    hide rdn with dissolve
    hide gyn with dissolve
    narrator 'Вы направляетесь к шатру, расположенному в центре лагеря.'

    scene blackness with dissolve
    pause 1
    narrator 'Этикет требует от Вас сперва постучаться, однако за неимением полноценной двери Вы просто откашливаетесь, надеясь тем самым предупредить хозяина о своем присутствии, и заходите. Ваши спутники следуют за Вами.'
    scene bg tent with dissolve
    pause 4
    narrator 'Шатер оказывается на удивление уютным, пол покрывают мягкие ковры, обстановку составляют несколько больших сундуков и разложенных всюду подушек, воздух пропитан ароматами душистых трав.'
    narrator 'Вы успеваете заметить множество высушенных растений, бутылок и склянок, а также всевозможных ступок и котелков.'
    narrator 'Ваши спутники останавливаются за Вашей спиной, не рискуя продвинуться дальше.'
    you 'Добрый день, простите за беспокойство!'
    show tsh neutral at center with dissolve
    narrator 'К вам выходит цыганка и окидывает всех троих проницательным взглядом. Она явно узнала Родиона и Гаянэ.'
    'Цыганка' 'Здравствуйте, господа. Вас послал граф?' (who_color="#7D587E")
    narrator 'Родион заметно нервничает, а Гаянэ делает вид, что не услышала вопроса, отчего Вам приходится ответить самостоятельно.'
    you 'И да,и нет. Простите, как я могу к Вам обращаться?'
    'Цыганка' 'Тшилаба. А Вас как величать?' (who_color="#7D587E")
    you 'Мое имя Дмитрий. Видите ли, Тшилаба, сегодня ночью мы обнаружили в усадьбе этот платок и хотели бы спросить у Вас, не известно ли Вам, кому он мог бы принадлежать?'
    show tsh surprise
    narrator 'Вы достаете платок и протягиваете его цыганке. Она удивленно и слегка надменно вскидывает одну бровь.'
    tsh 'Разумеется. Этот платок мой.'
    show gyn angry_pic with dissolve:
        xpos .05
        ypos .3
    gyn 'Так я и знала...'
    narrator 'Лицо Родиона искажается, и он не выдерживает.'
    show tsh neutral
    hide gyn with dissolve
    show rdn angry_pic with dissolve:
        xpos .05
        ypos .3
    rdn 'Так ты признаешь, что обокрала меня!'
    show tsh surprise
    narrator 'Тшилаба еще сильнее вскидывает брови, после чего лицо ее искажается гневом.'
    tsh angry 'Не стоит тебе на меня кричать, мальчик...'
    show rdn angry_pic2
    rdn 'Ты сегодня ночью вломилась в мою лабораторию и обворовала меня!'
    narrator 'Вы мельком бросаете взгляд на округлый живот цыганки. Едва ли беременная женщина могла устроить погром в лаборатории... Родиона эта мысль, кажется, не посетила.'
    tsh 'Да как смеешь ты обвинять меня в воровстве! По-твоему это смешно?! Ты думаешь, я не знаю, чем ты со своими склянками занимаешься в усадьбе?!'
    rdn angry_pic 'Я занимаюсь наукой, пока Вы... Вы занимаеесь грабежом!'
    narrator 'Родион и Тшилаба переходят на крик.'
    tsh glum 'Подлец! Я знаю, что ты там в своей норе делаешь лекарство от волчьей болезни, да только у тебя, неуча, ничего не выходит, вот ты и выкрал моё зелье!'
    tsh angry 'А теперь, когда у тебя же нашли платок, в котором ты его утащил, ты оговариваешь меня! Лжец!'
    you 'Постойте, прошу, Родион говорит о пропаже браслета...'
    tsh glum 'Какого, к черту, браслета?! Я не знаю, что этот мальчишка тебе наплел! Этот человек лжец и вор! Небось и не было никакого браслета, он просто себя выгораживает!'
    show rdn shock_pic
    rdn 'Неправда!'
    hide rdn with dissolve
    show yan shock at slightleft with dissolve
    narrator 'В шатер врывается молодой цыган. Похоже, он услышал крики спорщиков.'
    show yan angry
    'Цыган' 'Что здесь происходит? Выйдите из шатра!' (who_color = '#BDB56B')
    hide rdn with dissolve
    show tsh sad
    pause .1
    show yan shock_side
    narrator 'Он грубо выталкивает всех троих наружу и подходит к Тшилабе, пытаясь успокоить ее.'

    scene blackness with dissolve
    pause .3
    narrator 'Снаружи Вы слышите, как они переговариваются между собой.'
    scene bg camp2 with dissolve
    narrator 'Наконец, он выходит из шатра, один.'
    show yan neutral with dissolve
    'Цыган' 'Нам не нужны проблемы. Пожалуйста, уходите.' (who_color = '#BDB56B')
    show yan angry
    narrator 'Цыган смеряет взглядом Родиона. Тот, как ни странно, молчит и, кажется, не намерен продолжать спор.'
    show yan shock
    'Цыган' 'Мы здесь с разрешения графа и никому не мешаем. Если есть какие-то проблемы, все вопросы мы будем решать через графиню.' (who_color = '#BDB56B')
    show gyn angry_pic2 with dissolve:
        xpos .05
        ypos .3
    gyn 'О да, тебе бы этого хотелось!'
    show yan angry
    'Цыган' 'Не будь ты женщиной...' (who_color = '#BDB56B')
    hide gyn with dissolve
    you 'Прошу, не недо. Мы пришли только поговорить.'
    'Цыган' 'Поговорили. Все дальнейшие разговоры только через графиню. Уходите.' (who_color = '#BDB56B')
    show yan neutral
    narrator 'Вы замечаете, что прочие обитатели лагеря заметили вас и начинают медленно стекаться к шатру. Вы рассуждаете, что в такой обстановке не стоит продолжать конфликт, и решаете молча уйти вместе со своими спутниками.'









    return
